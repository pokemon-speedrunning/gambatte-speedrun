global_cflags = ARGUMENTS.get('CFLAGS', '-Wall -Wextra -O2 -fomit-frame-pointer')
global_cxxflags = ARGUMENTS.get('CXXFLAGS', global_cflags + ' -fno-exceptions -fno-rtti')
global_defines = ' -DHAVE_STDINT_H'
vars = Variables()
vars.Add('CC')
vars.Add('CXX')

env = Environment(CPPPATH = ['src', 'include', '../common'],
                  CFLAGS = global_cflags + global_defines,
                  CXXFLAGS = global_cxxflags + global_defines,
                  variables = vars)

sourceFiles = Split('''
			src/bitmap_font.cpp
			src/cpu.cpp
			src/gambatte.cpp
			src/initstate.cpp
			src/interrupter.cpp
			src/interruptrequester.cpp
			src/loadres.cpp
			src/memory.cpp
			src/sound.cpp
			src/state_osd_elements.cpp
			src/statesaver.cpp
			src/tima.cpp
			src/video.cpp
			src/mem/cartridge.cpp
            src/mem/huc3.cpp
			src/mem/memptrs.cpp
			src/mem/pakinfo.cpp
			src/mem/rtc.cpp
			src/mem/sgb.cpp
			src/mem/time.cpp
			src/sound/channel1.cpp
			src/sound/channel2.cpp
			src/sound/channel3.cpp
			src/sound/channel4.cpp
			src/sound/duty_unit.cpp
			src/sound/envelope_unit.cpp
			src/sound/length_counter.cpp
			src/video/ly_counter.cpp
			src/video/lyc_irq.cpp
			src/video/next_m0_time.cpp
			src/video/ppu.cpp
			src/video/sprite_mapper.cpp
		   ''')

conf = env.Configure()

if conf.CheckHeader('zlib.h'):
	sourceFiles.append('src/file/unzip/unzip.c')
	sourceFiles.append('src/file/unzip/ioapi.c')
	sourceFiles.append('src/file/file_zip.cpp')
else:
	sourceFiles.append('src/file/file.cpp')

conf.Finish()

lib = env.Library('gambatte', sourceFiles)

def rev():
	try:
		from subprocess import check_output
		stdout = check_output(['git', 'rev-list', 'HEAD', '--count'])
		return ' -DREVISION=' + stdout.strip()
	except:
		return ' -DREVISION=-1'

import sys
sys_libs = ['z']
if sys.platform == 'darwin':
	sys_libs.append('System')

slib = env.SharedLibrary('gambatte', sourceFiles + ['src/cinterface.cpp'],
                         CXXFLAGS = env['CXXFLAGS'] + ' -DDLLABLES' + rev(),
                         LINKFLAGS = env['LINKFLAGS'] + ' -static -s',
                         LIBS = sys_libs)

env.Default(lib)
env.Alias('slib', slib)
